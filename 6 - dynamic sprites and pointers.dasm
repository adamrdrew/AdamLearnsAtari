; Dynamic Sprites and Pointers
; OK, so we know how to display a player sprite. But in a real game we're going to need to change the sprite out
; either for animation, or changing the sprite at different states or whatever. And doing that will require a bit
; of work. We'll need a couple things:
;	1. WE NEED POINTERS! Right now our buffer code gets written into just by copying data from SpriteFaceRight. We're going to need multiple sprites
;		and a pointer that lets us read from whatever sprite is currently selected
;	2. We will need to refactor our scanline counter. In order to implement pointers we're going to need to use some op codes that
;		require use of the Y register, which we've blocked off as the scanline counter. Lets move that into memory
;
; My reference for this is "Making Games for the Atari 2600" Hugg, ch 14 "Indirect Adressing". I knew I needed pointers but had no idea
; how they'd be implemented. The book provides the same sort of use case and code as I have here. HOWEVER, the example provided in that
; book doesn't work in DASM. They have a whole different compiler they use on their 8bitworkshop.com website where the code lives. 
; So, it took a lot of trial and error to land on the right way to do it in DASM!
;
; The key parts are:
;	1. Create the 16 bit pointer in zero page. Lines 28 & 29
;	2. Set the pointer to the address of one of the sprite tables. See line 89 and SET_POINTER macro
;	3. Switch our sprite line buffer load code to use inderct addressesing. See line 207
;	4. Refactored so that scanline counting happens in ScanlineCounter and Y is freed up for doing register work, such as like 207

	processor 6502
	include vcs.h
	include macro.h
	org $F000

;This took a long time to figure out. None of the resources I consulted explained this properly.
;We need to create the pointer, but it needs to be in the zero page in order to work with lda indirect with y
;With DASM that means defining its address and then its length like so
;The book uses a different compiler and it handles it differently.
PlayerSpritePointer	= $99	
	.word;

;The number of scanlines on the visible portion of the screen
ScanlineCount = $80
;The current scanline we're on
ScanlineCounter = $92


VBlankTimer = $81
OverscanTimer = $82

PlayerSpriteY = $83
SpriteSliceToDraw = $84

InputUp = $85
InputDown = $86
InputLeft = $87
InputRight = $88

HorizMotionLeft = $89
HorizMotionRight = $90

PlayerSpriteBuffer = $91

Start
	CLEAN_START
	lda #$AA
	sta COLUBK
	lda #$34
	sta COLUP0

	lda #192
	sta ScanlineCount
	sta ScanlineCounter
	lda #43
	sta VBlankTimer
	lda #35
	sta OverscanTimer
	lda #75
	sta PlayerSpriteY
	lda #0
	sta SpriteSliceToDraw

	lda #%00010000
	sta InputUp
	lda #%00100000
	sta InputDown
	lda #%01000000
	sta InputLeft
	lda #%10000000
	sta InputRight

	lda #$10
	sta HorizMotionLeft
	lda #$F0
	sta HorizMotionRight



	;SET_POINTER is a nice macro provided by DASM, it isn't 6502 asm
	SET_POINTER PlayerSpritePointer, SpriteFaceRight
	;This is what it is doing under the hood
	;Mem addresses are 16bit on 6502, so we need to load the LSB and MSB of the address we want to point to into the pointer
	;SET_POINTER macro link: https://github.com/munsie/dasm/blob/45fa6482594af347152a97d684fd01bc0060bf1a/machines/atari2600/macro.h#L118
	;lda #<SpriteFaceRight
	;sta PlayerSpritePointer
	;lda #>SpriteFaceRight
	;sta PlayerSpritePointer+1

VSync
	sta WSYNC
	lda #%0010
	sta VSYNC
	sta WSYNC
	sta WSYNC
	sta WSYNC
	lda #0
	sta VSYNC

VBlankStart
	lda #%0010
	sta VBLANK
	lda VBlankTimer
	sta TIM64T

VBlankLogic
	;Do vblank stuff here
	lda ScanlineCount
	sta ScanlineCounter
	;Set the number of lines of player sprite to draw to 0
	;this ensures we skip drawing until the right time
	lda #0
	sta SpriteSliceToDraw

CheckMoveUp
	lda InputUp
	bit SWCHA
	bne CheckMoveDown
	inc PlayerSpriteY
	SET_POINTER PlayerSpritePointer, SpriteFaceUp

CheckMoveDown
	lda InputDown
	bit SWCHA
	bne CheckMoveLeft
	dec PlayerSpriteY
	SET_POINTER PlayerSpritePointer, SpriteFaceDown


CheckMoveLeft
	;We'll use register X for our horizontal movement speed
	;Default horizontal speed is 0
	ldx #0
	lda InputLeft
	bit SWCHA
	bne CheckMoveRight
	ldx HorizMotionLeft
	lda #$0B
	sta REFP0
	SET_POINTER PlayerSpritePointer, SpriteFaceRight

CheckMoveRight
	lda InputRight
	bit SWCHA
	bne SetHorizontalMovement
	ldx HorizMotionRight
	lda #0
	sta REFP0
	SET_POINTER PlayerSpritePointer, SpriteFaceRight

SetHorizontalMovement
	stx HMP0	


VBlankEnd
	lda INTIM
	bne VBlankEnd
	lda #0
	sta VBLANK
	sta WSYNC
	sta HMOVE


HBlank
	;Do pre-line stuff here
	sta WSYNC

DrawAndClearPlayerBuffer
	;Draw the player buffer to the screen and then clear it out
	;This is our only draw call, so this is the only thing that would
	;add any cycle overhead to the currently rendering line
	lda PlayerSpriteBuffer
	sta GRP0
	lda #0
	sta PlayerSpriteBuffer
	;Now we have a whole glorious scanline worth of CPU to do whatever we want for the next line!


DrawScanLine

CheckPlayerOnScanline
	;See if this is the line we should draw at
	ldx ScanlineCounter
	cpx PlayerSpriteY
	;If it isn't the line we should start drawing the player sprite at move on
	bne BufferPlayerSprite
	;If it is the line we should start drawing at set the lines of sprite to draw to 8
	lda #8
	sta SpriteSliceToDraw

BufferPlayerSprite
	;Load the sprite line count
	ldy SpriteSliceToDraw
	cpy #0
	;If the line count is 0 there's nothing to draw so skip drawing
	beq SkipPlayerSprite
	;Load the sprite line and buffer it 
	lda (PlayerSpritePointer),y
	sta PlayerSpriteBuffer
	;Decrement and store the sprite draw line
	dey
	sty SpriteSliceToDraw
SkipPlayerSprite

FinishScanline
	dec ScanlineCounter
	bne HBlank

OverscanStart
	lda #%0010
	sta VBLANK
	lda OverscanTimer
	sta TIM64T

OverscanLogic
	;Do overscan stuff here

OverscanEnd
	lda INTIM
	bne OverscanEnd
	lda #0
	sta VBLANK
	sta WSYNC 
	jmp VSync
	
SpriteFaceRight
	.byte %00000000 ;
	.byte %01111110 ;
	.byte %11111111 ;
	.byte %11111000 ;
	.byte %11110111 ;
	.byte %11111111 ;
	.byte %11111001 ;
	.byte %11111001 ;
	.byte %01111110 ;
SpriteFaceDown
	.byte %00000000 ;
	.byte %01111110 ;
	.byte %11000011 ;
	.byte %10111101 ;
	.byte %11111111 ;
	.byte %10011001 ;
	.byte %10011001 ;
	.byte %11111111 ;
	.byte %01111110 ;
SpriteFaceUp
	.byte %00000000 ;
	.byte %01111110 ;
	.byte %11111111 ;
	.byte %11111111 ;
	.byte %11111111 ;
	.byte %11111111 ;
	.byte %11111111 ;
	.byte %11111111 ;
	.byte %01111110 ;

	org $FFFC
	.word Start
	.word Start
